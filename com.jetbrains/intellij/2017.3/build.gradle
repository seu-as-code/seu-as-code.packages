buildscript {
    repositories {
        mavenCentral()
		maven { 
			url 'https://dl.bintray.com/seu-as-code/maven'
		}
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:2.0.0'
    }
}
plugins {    
    id 'de.qaware.seu.as.code.credentials' version '2.4.0'
}
apply plugin: 'base'
apply plugin: 'maven-publish'
import de.undercouch.gradle.tasks.download.Download

task downloadArchive(type: Download) {
    src "https://download-cf.jetbrains.com/idea/ideaIU-${version}.win.zip"
    dest buildDir
}

task extractArchive(type: Copy, dependsOn: downloadArchive) {
    from { zipTree("$buildDir/ideaIU-${version}.win.zip") }
    into "$buildDir/files/intellij-${version}"
}

task deleteIntelljJDK(type: Delete, dependsOn: extractArchive) {
  delete "$buildDir/files/jre32", "$buildDir/files/jre64"
  followSymlinks = true
}

task customize(type: Copy, dependsOn: deleteIntelljJDK) {
	from "files"
	into "$buildDir/files"
}

task buildPackage(type: Jar, dependsOn: deleteIntelljJDK) {
	baseName project.name
	version version
    classifier 'win'
	extension 'jar'
	destinationDir buildDir
	from "$buildDir/files"
}

publishing {
  publications {
		intellij(MavenPublication) {
			artifact buildPackage
		}
  }
	repositories {
		maven {
			url = project.hasProperty('nexusUrl') ? project.nexusUrl : ''
			credentials {	
				if(project.credentials.get("nexus")!=null){
					username = project.credentials.get('nexus').username
					password = project.credentials.get('nexus').password
				}
				else {
					username = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
					password = project.hasProperty('nexusPassword') ? project.nexusPassword : ''
				} 					
			}
		}
    }
}
