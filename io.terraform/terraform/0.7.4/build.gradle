plugins {
    id 'de.undercouch.download' version '1.2'
    id 'com.jfrog.bintray' version '1.2'
}

apply plugin: 'base'
apply plugin: 'maven-publish'

import de.undercouch.gradle.tasks.download.Download

task downloadArchive(type: Download) {
    src "https://releases.hashicorp.com/terraform/0.7.4/terraform_0.7.4_windows_amd64.zip"
    dest buildDir
}

task mkdirs << {
	file("$buildDir/files/terraform-0.7.4").mkdirs()
}

task extractArchive(type: Copy, dependsOn: [mkdirs, downloadArchive]) {
    from zipTree(file("$buildDir/terraform_0.7.4_windows_amd64.zip"))
    into file("$buildDir/files/terraform-0.7.4")
}

task customizePackage(type: Copy, dependsOn: extractArchive) {
    from "files"
    into "$buildDir/files"
}

task buildPackage(type: Jar, dependsOn: customizePackage) {
    baseName project.name
    version version
    extension 'jar'
	classifier 'x64'
    destinationDir buildDir
    from "$buildDir/files"
}

publishing {
    publications {
        terraform074(MavenPublication) {
            artifact buildPackage
        }
    }
    repositories {
        // set the properties via -P to publish to your company repo
        maven {
            url = project.hasProperty('nexusUrl') ? project.nexusUrl : ''
            credentials {
                username = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
                password = project.hasProperty('nexusPassword') ? project.nexusPassword : ''
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : 'unknown'
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : 'unknown'
    publications = ['terraform074']
	  publish = true
	  dryRun = false
    
    pkg {
        repo = project.bintrayRepo
        name = project.name
        desc = project.description
        licenses = ['MPL 2.0']
        labels = ['terraform', 'provisioning', 'seu-as-code', 'package']
		websiteUrl = project.websiteUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.scmUrl
        publicDownloadNumbers = true

        version {
            name = project.version
			      desc = "${project.description} ${project.version}"
			      released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")

      			mavenCentralSync {
      				sync = false
      			}
        }
    }
}